plugins {
    id 'java-library'
    id 'penna.publishing'
    id 'penna.build.projectVersion'
    id 'pmd'
}

group 'com.hkupty.penna'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// Reproducible builds
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

// The API is an optional client-facing library and therefore it should not have gaps
// in the documentation, nor fail on linting, so it is easier for consumers to understand
// what it is doing under the hood.
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += [
            "-Xlint:all",
            "-Xdoclint:all/public",
            "-Werror"
    ]
}


// version = "0.8.1"

dependencies {
    compileOnly libs.slf4j
    compileOnly libs.jetbrains.annotations

    testImplementation libs.junit.api
    testRuntimeOnly libs.junit.engine
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes('Implementation-Version': project.version)
    }
}

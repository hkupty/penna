import org.gradle.api.plugins.jvm.JvmTestSuite

plugins {
    id 'penna.base-java'
    id 'penna.publishing'
    id 'signing'
    id 'pmd'
    id 'jvm-test-suite'
}

group 'com.hkupty.penna'
version '0.6.2'

repositories {
    mavenCentral()
}

pmd {
    sourceSets = [sourceSets.main]
    ruleSets("category/java/performance.xml", "category/java/bestpractices.xml")
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }

        propertyTesting(JvmTestSuite) {
            testType = "PropertyBasedTesting"

            dependencies {
                implementation project(":penna-api")
                implementation project(":penna-core")
                implementation 'org.slf4j:slf4j-api:2.0.9'
                implementation 'net.jqwik:jqwik:1.7.2'
                implementation 'org.apache.commons:commons-math3:3.6.1'
                implementation 'com.fasterxml.jackson.core:jackson-core:2.14.0'
                implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0'
                compileOnly 'org.jetbrains:annotations:23.0.0'
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
        jmh(JvmTestSuite) {
            testType = TestSuiteType.PERFORMANCE_TEST
            dependencies {
                implementation project(":penna-api")
                implementation project(":penna-core")
                implementation 'org.slf4j:slf4j-api:2.0.9'
                implementation 'org.openjdk.jmh:jmh-core:1.37'
                implementation 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
                implementation 'com.fasterxml.jackson.core:jackson-core:2.15.2'
                implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
                implementation 'ch.qos.logback:logback-core:1.4.11'
                implementation 'ch.qos.logback:logback-classic:1.4.11'
                implementation 'ch.qos.logback.contrib:logback-jackson:0.1.5'
                implementation 'ch.qos.logback.contrib:logback-json-core:0.1.5'
                implementation 'ch.qos.logback.contrib:logback-json-classic:0.1.5'
                implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

                annotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                        shouldRunAfter(propertyTesting)
                    }
                }
            }
        }
    }
}

tasks.named("check") {
    dependsOn(testing.suites.propertyTesting)
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation project(":penna-api")

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'com.fasterxml.jackson.core:jackson-core:2.14.2'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'

}

plugins {
    id 'java'
}

group 'penna'
version '0.1'

repositories {
    mavenCentral()
}

configurations {
    sharedRuntime
    runtimePenna
    runtimeLogback
}

task runPenna(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    classpath configurations.runtimePenna
    mainClass = "penna.throughput.Main"
}

task buildPennaJar(type: Jar) {
    from sourceSets.main.output
    dependsOn sourceSets.main.runtimeClasspath
    dependsOn configurations.runtimePenna
    manifest{
        attributes 'Main-Class': "penna.throughput.Main"
    }
    archiveBaseName = "pennaJar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimePenna.collect { it.isDirectory() ? it : zipTree(it) } +
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

task runLogback(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    classpath configurations.sharedRuntime
    classpath configurations.runtimeLogback
    mainClass = "penna.throughput.Main"
}

task buildLogbackJar(type: Jar) {
    from sourceSets.main.output
    dependsOn sourceSets.main.runtimeClasspath
    dependsOn configurations.runtimeLogback
    manifest{
        attributes 'Main-Class': "penna.throughput.Main"
    }
    archiveBaseName = "logbackJar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeLogback.collect { it.isDirectory() ? it : zipTree(it) } +
                configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.6'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.14.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    implementation 'org.apache.commons:commons-lang3:3.13.0'

    runtimePenna project(":penna-core")

    runtimeLogback 'ch.qos.logback:logback-classic:1.4.11'
}

test {
    useJUnitPlatform()
}

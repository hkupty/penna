plugins {
    id 'java'
    id 'application'
}

group = 'org.example'
version = '0.1' // version is not important for the sample app

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// Reproducible builds
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += [
            "-Xlint:deprecation",
            "-Xlint:removal",
            "-Xlint:unchecked",
            // TODO Eventually we should enable -Werror here
            //"-Werror"
    ]
}

dependencies {
    implementation libs.slf4j
    implementation project(":penna-api")
    implementation project(":penna-core")
    runtimeOnly project(":penna-yaml-config")
    runtimeOnly libs.jackson.core
    runtimeOnly libs.jackson.databind
    runtimeOnly libs.jackson.yaml
}

application {
  mainClass = 'org.example.Main'
}
